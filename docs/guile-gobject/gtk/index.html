<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
 <head>
  <title>(guile gtk): docs: guile-gobject: gtk</title>
  <style type="text/css"><!--
  /* bad netscape four, no stylesheet for you */
  @import url(../../../base.css);
  --></style>
 </head>
 <body>
  <div id="body">
   <div id="heading">

    <h1>(guile gtk)</h1>
    <div id="menu-bar" class="reversed"><a href="../../../">top</a> <a  href="../../../docs/">documentation</a> <a  href="../../../download/">download</a> <a  href="../../../dev/">developers</a> <a  href="../../../contact/">contact</a> <a  href="../../../links/">links</a></div>

   </div>
   <div id="text">

    <h2>(help (gnome gtk))</h2>

    <h3>hello world</h3>

    <p>
     The following code can be found in <code>examples/gtk/hello.scm</code>.
    </p>

<pre>
;; load up gtk -- also pulls in goops bindings
(use-modules (gnome gtk))

;; define the app as a function -- there are many other ways to do this,
;; of course...
(define (app)
  ;; we can make new widgets just like we make goops objects -- there is
  ;; a corresponding goops class for every GType we know about. the
  ;; arguments to make, after the class, are interpreted as properties
  ;; to set. in this case we make a toplevel window and a button with
  ;; the label, "Hello, World!".
  (let* ((window (make &lt;gtk-window&gt; #:type 'toplevel))
	 (button (make &lt;gtk-button&gt; #:label "Hello, World!")))

    ;; there are scheme functions corresponding to all of the c ones...
    ;; and of course we don't have to cast anything.
    (gtk-container-set-border-width window 10)
    (gtk-container-add window button)
    
    ;; and of course you can attach a lambda to a signal :-)
    (gobject-signal-connect button 'clicked (lambda (b) (gtk-main-quit)))

    (gtk-widget-show-all window)

    (gtk-main)))

(app)
</pre>

    <h3>hello world, improved</h3>

    <p>
     There's something about this that resembles programming in C a little bit
     too much... In Python, you can just do window.add(button). That's a lot
     less typing. In GOOPS, the object framework for guile, methods are
     implemented a bit differently. The <code>(gnome gtk generics)</code>
     module defines methods for those gtk functions described with
     `define-method' in the <code>.defs</code> file, so
     <code>hello-generics.scm</code> looks a bit different:
    </p>

<pre>
(use-modules (gnome gtk))

(define (app)
  (let* ((window (make &lt;gtk-window&gt; #:type 'toplevel))
	 (button (make &lt;gtk-button&gt; #:label "Hello, World!")))

    ;; up to here, everything is the same as hello.scm. however, now we
    ;; can make use of generic functions:

    ;; since window is a container, this generic maps onto the function
    ;; gtk-container-set-border-width
    (set-border-width window 10)

    ;; note that we can set the border width with a gobject property as
    ;; well:
    (gobject-set-property window 'border-width 15)

    ;; (gnome gobject generics), re-exported by (gnome gtk), defines a
    ;; generic `set' method for gobject-set-property, se we can also do
    ;; it like this:
    (set window 'border-width 20)

    ;; this is much less typing :-)
    (add window button)
    
    ;; see (gnome gobject generics) for a full list of gobject generic
    ;; functions
    (connect button 'clicked (lambda (b) (gtk-main-quit)))

    ;; generic functions for .defs apis are defined in the .defs files,
    ;; not manually
    (show-all window)

    (gtk-main)))

(app)
</pre>

    <p>
     That's it for the docs. They're not quite adequate -- want to write some
     more? <a href="../../../contact/">Contact us</a>!
    </p>
   </div>

<p></p>
<div class="copyright">
<p>
Copyright (C) 2003 
Free Software Foundation, Inc., 59 Temple Place - Suite 330, Boston, 
MA 02111, USA
</p>

<p>
Verbatim copying and distribution of this entire article is
permitted in any medium, provided this notice is preserved.
<br />
Updated:

<!-- timestamp start -->
$Date: 2003/10/02 15:19:29 $ $Author: mvo $
<!-- timestamp end -->
</p>
</div>

  </div>
 </body>
</html>
